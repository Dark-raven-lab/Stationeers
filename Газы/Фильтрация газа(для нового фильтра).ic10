define kMaxPresInGasTank 15000 # kpA
# For Start Filtration
define kMaxTemp 130 # C
define kMinTemp -50 # C
define kMinRatio 0.01 # 0.01 = 1%
# DEVICES
alias Slider d0 # optional
alias WarningLamp d1 # optional
# REGISTERS
alias warning r15
alias lightState r14
alias filterState r13
alias typeGas r12
Start:
move typeGas 0
s db Mode 1 # enable for one tick
yield
# Check Gas Output
l r0 db RatioCarbonDioxideOutput
breqz r0 2 # if r1 == 0 then +3 line
move typeGas 1 # Co2
l r0 db RatioNitrogenOutput
breqz r0 2 # if r1 == 0 then +3 line
move typeGas 2 # N2
l r0 db RatioOxygenOutput
breqz r0 2 # if r1 == 0 then +3 line
move typeGas 3 # O2
l r0 db RatioVolatilesOutput
breqz r0 2 # if r1 == 0 then +3 line
move typeGas 4 # H2
l r0 db RatioNitrousOxideOutput
breqz r0 2 # if r1 == 0 then +3 line
move typeGas 5
l r0 db RatioPollutantOutput
breqz r0 2 # if r1 == 0 then +3 line
move typeGas 6
beqz typeGas Start
# contune
s db Open 0
# Setting devices
brdns Slider 2
s Slider Lock 1
Main:
yield
l r0 db Open
beqz r0 CheckFilters
# DISABLED SYSTEM
s db Mode 1
s db On 0
yield
j 1 # Initialization
CheckFilters:
ls r0 db 0 Occupied
ls r1 db 1 Occupied
add r2 r0 r1
max r2 r2 1
ls r0 db 0 Quantity
ls r1 db 1 Quantity
add r0 r0 r1 # summ, 0-200
div r0 r0 r2 # x / count filters
mul r0 r0 0.01 # x * 0.01
slt r1 r0 0.2 # 1 if x < 20%
SetSlider:
brdns Slider 3 # skip if not connected
s Slider Setting r0 # precent
s Slider On r1 # enable/disable
seqz warning r0 # 1 if x == 0

# tank control
l r0 db PressureOutput

# 1 if GasTank.Pressure < kMaxPresInGasTank
slt filterState r0 kMaxPresInGasTank
beqz filterState SetFilter # EXIT

# 1 warning == 0
seqz filterState warning
beqz filterState SetFilter # EXIT

l r0 db TemperatureInput
sub r0 r0 273.15

# 1 if DirtyTank.Temp < Max Gas Temp
slt filterState r0 kMaxTemp
beqz filterState SetFilter # EXIT

# 1 if DirtyTank.Temp > Min Gas Temp
sgt filterState r0 kMinTemp
beqz filterState SetFilter # EXIT

GetGasRatio:
move r0 0
brne typeGas 1 2
l r0 db RatioCarbonDioxideInput
brne typeGas 2 2
l r0 db RatioNitrogenInput
brne typeGas 3 2
l r0 db RatioOxygenInput
brne typeGas 4 2
l r0 db RatioVolatilesInput
brne typeGas 5 2
l r0 db RatioNitrousOxideInput
brne typeGas 6 2
l r0 db RatioPollutantInput
SelectState:
l filterState db Mode
select r1 filterState 0 kMinRatio
sgt filterState r0 r1
SetFilter:
s db Mode filterState
Warning:
# if (warningOn == 1) { Flicker On }
breqz warning 4 # exit if warningOn == 0
move lightState 1
s WarningLamp On 1
j Main
# else if (warningOn == 0 && oldState == 1)
# { Flicker Off; oldState = warningOn }
seqz r0 warning # warningOn == 0
seq r1 lightState 1 # oldState == 1
and r0 r0 r1 # r0 = 1 && r1 == 1
beqz r0 Main # exit if r0 == 0
move lightState 0
s WarningLamp On 0
j Main