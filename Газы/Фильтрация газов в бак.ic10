define MaxPresInGasTank 25000 # pA
define MinTempinDirtyTank 298 # Kelvin
define MaxTempInGasTank 318 # 45 C

alias GasTank d0
alias Filter d1
alias Slider d2
alias DirtyTank d3
alias FlickerLamp d4 # optional

s Slider Lock 1

alias warningOn r15
alias oldState r14
alias filterEnable r13

Main:
yield
# check filters
ls r0 Filter 0 Quantity
ls r1 Filter 1 Quantity
add r0 r0 r1 # summ, 0-200
div r0 r0 2 # x / 2
mul r0 r0 0.01 # x * 0.01
s Slider Setting r0
slt r1 r0 0.5 # 1 if x < 0.5
s Slider On r1 # enable/disable
seqz warningOn r0 # 1 if x == 0

# tank control
# GasTank.Pressure < MaxPresInGasTank
l r0 GasTank Pressure
slt r0 r0 MaxPresInGasTank # <

# DirtyTank.Temp < MinTempinDirtyTank
l r1 DirtyTank Temperature
slt r1 r1 MinTempinDirtyTank # <
add r0 r0 r1

# GasTank.Temp < MaxTempInGasTank
l r1 GasTank Temperature
slt r1 r1 MaxTempInGasTank
add r0 r0 r1

# 1 if warningOn == false
select r1 warningOn 0 1
add r0 r0 r1

# if pressNotZero
jal CheckGas
snez r1 r1 # 1 if r1 != 0
add r0 r0 r1
# checking
seq filterEnable r0 5
s Filter On filterEnable

bdseal FlickerLamp Warning
j Main

CheckGas:
# NOT USE REGISTER r0
l r1 GasTank RatioPollutant
breqz r1 3 # if r1 == 0 then +3 line
l r1 DirtyTank RatioPollutant
j ra

l r1 GasTank RatioCarbonDioxide
breqz r1 3 # if r1 == 0 then +3 line
l r1 DirtyTank RatioCarbonDioxide
j ra

l r1 GasTank RatioNitrogen
breqz r1 3 # if r1 == 0 then +3 line
l r1 DirtyTank RatioNitrogen
j ra

l r1 GasTank RatioNitrousOxide
breqz r1 3 # if r1 == 0 then +3 line
l r1 DirtyTank RatioNitrousOxide
j ra

l r1 GasTank RatioOxygen
breqz r1 3 # if r1 == 0 then +3 line
l r1 DirtyTank RatioOxygen
j ra

l r1 GasTank RatioVolatiles
breqz r1 3 # if r1 == 0 then +3 line
l r1 DirtyTank RatioVolatiles
j ra
# if tank empty
move r1 0
j ra

Warning:
# if (warningOn == 1) { Flicker On }
breqz warningOn 4 # exit if warningOn == 0
move oldState 1
s FlickerLamp On 1
j ra
# else if (warningOn == 0 && oldState == 1)
# { Flicker Off; oldState = warningOn }
seqz r0 warningOn # warningOn == 0
seq r1 oldState 1 # oldState == 1
and r0 r0 r1 # r0 = 1 && r1 == 1
beqz r0 ra # exit if r0 == 0
move oldState 0
s FlickerLamp On 0
j ra