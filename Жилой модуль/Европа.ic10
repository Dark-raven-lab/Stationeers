define kAccuracyPressure 10 # kPa
define kAutoMode 0 # if 1 then need 3 gas sensor
define kSLIDER 576516101
define kALARMLAMP -1535893860
define kVOICE -828056979
define kGASSENSOR -1252983604
define kAIRVENT -1129453144
define kVENTEXT HASH("Ext.Active Vent")
define kVENTINT HASH("Int.Active Vent")
alias DoorInt d0
alias DoorExt d1
alias Gas d2
alias Led d5
alias pressInt r14
alias pressExt r13
alias pressOut r12
alias skipAirLook r11
alias hashVentBack r10
alias hashVentFront r9
sb kSLIDER Lock 0
sb kSLIDER On 0
s DoorInt Setting 0
s DoorExt Setting 0
l r0 DoorInt Open
select r1 r0 0 1
s DoorInt On r1
s DoorExt On r0
s DoorExt Open r1
s DoorInt Lock 0
s DoorExt Lock 0
s DoorInt Mode 1
s DoorExt Mode 1
Main:
yield
beqz kAutoMode CheckDoors
lb r0 kGASSENSOR Pressure Maximum
lb r1 kGASSENSOR Pressure Minimum
sub r0 r0 r1
slt r3 r0 50
lb r0 kGASSENSOR Temperature Maximum
lb r1 kGASSENSOR Temperature Minimum
sub r0 r0 r1
slt r4 r0 15
and r0 r3 r4
sb kSLIDER On r0
yield
CheckDoors:
l r1 DoorInt Setting
l r0 DoorExt Setting
lb skipAirLook kSLIDER On Maximum
beq r1 1 GoInt # if Open
beq r0 1 GoExt # if Open
j Main
GoInt:
alias DoorBack d1 # Ext
alias DoorFront d0 # Int
l pressInt Gas Pressure # Save cur pressure
move pressOut pressExt
j AirLookReady
GoExt:
alias DoorBack d0 # Int
alias DoorFront d1 # Ext
l pressExt Gas Pressure # Save cur pressure
move pressOut pressInt
AirLookReady:
select hashVentBack r0 kVENTINT kVENTEXT
select hashVentFront r1 kVENTINT kVENTEXT
AirLook:
s DoorFront Setting 0
bgtz skipAirLook CloseDoor
l r0 DoorFront On
sb kVOICE On r0
sb kALARMLAMP On r0
sb kSLIDER On r0
sb kSLIDER Lock r0
s DoorBack Lock r0
sbn kAIRVENT hashVentBack Lock r0
sbn kAIRVENT hashVentFront Lock r0
brdns Led 3
select r1 r0 4 3
s Led Color r1
beqz r0 Main # exit after circle
sb kVOICE Mode 9
CloseDoor:
s DoorBack On 1 # enable door in
s DoorBack Open 0 # closed door in
sleep 0.5
bgtz skipAirLook OpenDoor
StartDepressure:
l r2 Gas Pressure
sbn kAIRVENT hashVentBack Mode 1 # set mode
sbn kAIRVENT hashVentFront On 0
WaitDepressure:
yield
l r15 Gas Pressure # check pressure
div r0 r15 r2
sb kSLIDER Setting r0
sgtz r0 r15 # enable if pressure > 0
l r1 DoorFront Setting
xor r0 r0 r1 # !
sbn kAIRVENT hashVentBack On r0
s DoorFront Setting 0
bgtz r0 WaitDepressure # if press != 0 retern
Pressurization:
sbn kAIRVENT hashVentFront Mode 0 # First!
WaitPressurization:
yield
sbn kAIRVENT hashVentFront PressureExternal pressOut
l r15 Gas Pressure
sub r1 pressOut kAccuracyPressure # Need pressure
div r0 r15 r1
sb kSLIDER Setting r0
slt r0 r15 r1
l r1 DoorFront Setting
xor r0 r0 r1
sbn kAIRVENT hashVentFront On r0
s DoorFront Setting 0
bgtz r0 WaitPressurization
sb kVOICE Mode 25
OpenDoor:
yield
s DoorFront Open 1
s DoorFront On 0
s DoorBack On 1
bgtz skipAirLook Main
l r0 DoorFront On
beq r0 1 OpenDoor
j AirLook