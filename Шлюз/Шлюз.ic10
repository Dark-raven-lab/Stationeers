define kAccuracyPressure 10

define kSLIDER 576516101
define kALARMLAMP -1535893860
define kVOICE -828056979

alias DoorInt d0
alias VentInt d1
alias Gas d2
alias DoorExt d3
alias VentExt d4
alias Lamp d5
alias PressInt r15
alias PressExt r14
alias curPress r13
alias pressOut r12
Init:
sb kSLIDER Lock 0
sb kSLIDER On 0
s DoorInt Setting 0
s DoorExt Setting 0
l r0 DoorInt Open
select r1 r0 0 1
s DoorInt On r1
s DoorExt On r0
s DoorExt Open r1
s DoorInt Lock 0
s DoorExt Lock 0
move PressInt 0
move PressExt 0
Main:
yield
lb r0 kSLIDER On Maximum
seqz r0 r0 # 1 if == 0
s DoorInt Mode r0
s DoorExt Mode r0
bgtz r0 CheckDoor
s DoorInt On 1
s DoorExt On 1
j Main
CheckDoor:
l r1 DoorInt Setting
l r0 DoorExt Setting
beq r1 1 GoInt # if Open
beq r0 1 GoExt # if Open
j Main
GoInt:
l PressInt Gas Pressure # Save cur pressure
move pressOut PressExt
alias DoorBack d3
alias DoorFront d0
alias VentBack d4
alias VentFront d1
j Start
GoExt:
l PressExt Gas Pressure # Save cur pressure
move pressOut PressInt
alias DoorBack d0
alias DoorFront d3
alias VentBack d1
alias VentFront d4
Start:
sb kVOICE On 1
sb kVOICE Mode 9
s DoorFront Setting 0
sb kALARMLAMP On 1
brdns Lamp 2 # enable lamp if connected
s Lamp On 1
sb kSLIDER On 1
sb kSLIDER Lock 1
CloseDoor:
s DoorBack On 1 # enable door in
s DoorBack Open 0 # closed door in
sleep 1
s DoorBack On 0 # disable door in
StartDepressure:
l r1 Gas Pressure
s VentBack Lock 1
s VentBack Mode 1 # set mode depressure
s VentBack On 1 # START DEPRESSURE
s VentFront Lock 1
s VentFront On 0
WaitDepressure:
yield
l r0 DoorFront Setting # check skip stage
bgtz r0 Pressurization # If out want open-go next
l curPress Gas Pressure # check pressure
div r0 curPress r1
sb kSLIDER Setting r0
bgtz curPress WaitDepressure # if press != 0 retern
Pressurization:
s VentBack On 0 # STOP DEPRESSURE
s VentFront Mode 0 # First!
s VentFront PressureExternal pressOut
s VentFront On 1
s DoorFront On 1
WaitPressurization:
yield
l r0 DoorFront Setting # check skip stage
bgtz r0 OpenDoor # If door out was open - exit
l curPress Gas Pressure
sub r1 pressOut kAccuracyPressure # Need pressure
div r0 curPress r1
sb kSLIDER Setting r0
blt curPress r1 WaitPressurization
OpenDoor:
sb kVOICE Mode 25
s VentFront On 0
s VentFront Lock 0
s VentBack Lock 0
s DoorFront Open 1
sb kSLIDER On 0
sb kSLIDER Lock 0
sb kALARMLAMP On 0
sleep 1
sb kVOICE On 0
s DoorFront Setting 0
s DoorFront On 0
s DoorBack On 1
bdns Lamp Main # disable lamp if connected
s Lamp On 0
j Main