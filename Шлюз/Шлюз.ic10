define kAccuracyPressure 10
define kSLIDER 576516101
define kALARMLAMP -1535893860
define kVOICE -828056979
alias DoorInt d0
alias VentInt d1
alias Gas d2
alias DoorExt d3
alias VentExt d4
alias Lamp d5
alias PressInt r15
alias PressExt r14
alias curPress r13
alias pressOut r12
alias skipAirLook r11
Init:
sb kSLIDER Lock 0
sb kSLIDER On 0
s DoorInt Setting 0
s DoorExt Setting 0
l r0 DoorInt Open
select r1 r0 0 1
s DoorInt On r1
s DoorExt On r0
s DoorExt Open r1
s DoorInt Lock 0
s DoorExt Lock 0
s DoorInt Mode 1
s DoorExt Mode 1
Main:
yield
l r1 DoorInt Setting
l r0 DoorExt Setting
lb skipAirLook kSLIDER On Maximum
beq r1 1 GoInt # if Open
beq r0 1 GoExt # if Open
j Main
GoInt:
l PressInt Gas Pressure # Save cur pressure
move pressOut PressExt
alias DoorBack d3
alias DoorFront d0
alias VentBack d4
alias VentFront d1
j AirLook
GoExt:
l PressExt Gas Pressure # Save cur pressure
move pressOut PressInt
alias DoorBack d0
alias DoorFront d3
alias VentBack d1
alias VentFront d4
AirLook:
s DoorFront Setting 0
bgtz skipAirLook CloseDoor
l r0 DoorFront On
sb kVOICE On r0
sb kALARMLAMP On r0
sb kSLIDER On r0
sb kSLIDER Lock r0
s VentBack Lock r0
s VentFront Lock r0
brdns Lamp 2
s Lamp On r0
beqz r0 Main
sb kVOICE Mode 9
CloseDoor:
s DoorBack On 1 # enable door in
s DoorBack Open 0 # closed door in
sleep 0.5
s DoorBack On 0 # disable door in
bgtz skipAirLook OpenDoor
StartDepressure:
l r1 Gas Pressure
s VentBack Mode 1 # set mode depressure
s VentFront On 0
WaitDepressure:
yield
l curPress Gas Pressure # check pressure
div r0 curPress r1
sb kSLIDER Setting r0
sgtz r0 curPress # enable if pressure > 0
s VentBack On r0
bgtz r0 WaitDepressure # if press != 0 retern
Pressurization:
s VentFront Mode 0 # First!
s VentFront PressureExternal pressOut
WaitPressurization:
yield
l curPress Gas Pressure
sub r1 pressOut kAccuracyPressure # Need pressure
div r0 curPress r1
sb kSLIDER Setting r0
slt r0 curPress r1
l r1 DoorFront Setting # check skip stage
xor r0 r0 r1
s VentFront On r0
bgtz r0 WaitPressurization
sb kVOICE Mode 25
OpenDoor:
s DoorFront Open 1
sleep 1
s DoorFront On 0
s DoorBack On 1
s DoorFront Setting 0
bgtz skipAirLook Main
yield
j AirLook