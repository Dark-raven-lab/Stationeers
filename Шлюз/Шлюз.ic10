define kAccuracyPressure 10
define kAutoMode 0
define kSLIDER 576516101
define kALARMLAMP -1535893860
define kVOICE -828056979
define kGASSENSOR -1252983604
define kDOOR -2105052344
define kDOOREXT HASH("Ext.Airlock")
define kVENTEXT HASH("Ext.AirVent")
define kDOORINT HASH("Int.Airlock")
define kVENTINT HASH("Int.AirVent")
alias Gas d2
alias VentExt d4
alias Lamp d5
alias pressInt r14
alias pressExt r13
alias pressOut r12
alias skipAirLook r11
alias hashDoorBack r10
alias hashDoorFront r9
alias hashVentBack r8
alias hashVentFront r7
Init:
sb kSLIDER Lock 0
sb kSLIDER On 0
sbn kDOOR kDOOREXT Setting 0
sbn kDOOR kDOORINT Setting 0
lbn r0 kDOOR kDOORINT Open Sum
select r1 r0 0 1
sbn kDOOR kDOORINT On r1
sbn kDOOR kDOOREXT On r0
sbn kDOOR kDOOREXT Open r1
sbn kDOOR kDOORINT Lock 0
sbn kDOOR kDOOREXT Lock 0
sbn kDOOR kDOORINT Mode 1
sbn kDOOR kDOOREXT Mode 1
Main:
yield
beqz kAutoMode CheckDoors
lb r0 kGASSENSOR Pressure Maximum
lb r1 kGASSENSOR Pressure Minimum
sub r0 r0 r1
slt r3 r0 50
lb r0 kGASSENSOR Temperature Maximum
lb r1 kGASSENSOR Temperature Minimum
sub r0 r0 r1
slt r4 r0 15
and r0 r3 r4
sb kSLIDER On r0
yield
CheckDoors:
lbn r1 kDOOR kDOORINT Setting Maximum
lbn r0 kDOOR kDOOREXT Setting Maximum
lb skipAirLook kSLIDER On Maximum
beq r1 1 GoInt # if Open
beq r0 1 GoExt # if Open
j Main
GoInt:
l pressInt Gas Pressure # Save cur pressure
move pressOut pressExt
j AirLookReady
GoExt:
l pressExt Gas Pressure # Save cur pressure
move pressOut pressInt
AirLookReady:
select hashDoorBack r0 kDOORINT kDOOREXT
select hashDoorFront r1 kDOORINT kDOOREXT
select hashVentBack r0 kVENTINT kVENTEXT
select hashVentFront r1 kVENTINT kVENTEXT
AirLook:
sbn kDOOR hashDoorFront Setting 0
bgtz skipAirLook CloseDoor
lbn r0 kDOOR hashDoorFront On Maximum
sb kVOICE On r0
sb kALARMLAMP On r0
sb kSLIDER On r0
sb kSLIDER Lock r0
sbn -1129453144 hashVentBack Lock r0
sbn -1129453144 hashVentFront Lock r0
brdns Lamp 2
s Lamp On r0
beqz r0 Main
sb kVOICE Mode 9
CloseDoor:
sbn kDOOR hashDoorBack On 1 # enable door in
sbn kDOOR hashDoorBack Open 0 # closed door in
sleep 0.5
sbn kDOOR hashDoorBack On 0 # disable door in
bgtz skipAirLook OpenDoor
StartDepressure:
l r1 Gas Pressure
sbn -1129453144 hashVentBack Mode 1 # set mode depressure
sbn -1129453144 hashVentFront On 0
WaitDepressure:
yield
l r15 Gas Pressure # check pressure
div r0 r15 r1
sb kSLIDER Setting r0
sgtz r0 r15 # enable if pressure > 0
lbn r1 kDOOR hashDoorFront Setting Maximum
xor r0 r0 r1
sbn -1129453144 hashVentBack On r0
bgtz r0 WaitDepressure # if press != 0 retern
Pressurization:
sbn kDOOR hashDoorFront Setting 0
sbn -1129453144 hashVentFront Mode 0 # First!
sbn -1129453144 hashVentFront PressureExternal pressOut
WaitPressurization:
yield
l r15 Gas Pressure
sub r1 pressOut kAccuracyPressure # Need pressure
div r0 r15 r1
sb kSLIDER Setting r0
slt r0 r15 r1
lbn r1 kDOOR hashDoorFront Setting Maximum
xor r0 r0 r1
sbn -1129453144 hashVentFront On r0
bgtz r0 WaitPressurization
sb kVOICE Mode 25
OpenDoor:
sbn kDOOR hashDoorFront Open 1
sleep 1
sbn kDOOR hashDoorFront On 0
sbn kDOOR hashDoorBack On 1
sbn kDOOR hashDoorFront Setting 0
bgtz skipAirLook Main
yield
j AirLook