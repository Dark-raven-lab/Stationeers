# SETTINGS
define kAccuracyPressure 5 # Boost pressure

# !!! REQUIRED DEVICES !!!
define kGasSensor -1252983604
define kToggleButton 491845673
define kSlider 576516101
define kPrefHashHangarDoor -1351081801
define kPrefHashAirVent -785498334
define kHashNameAirVentInt HASH("Air Vent Internal")
define kHashNameAirVentExt HASH("Air Vent External")
alias DoorInt d0

# OPTIONAL DEVICES
define kFlickerLamp -1535893860
define kSpeaker -828056979

# REGISTERS
alias pressInt r15
alias pressExt r14
alias curPress r13
alias pressOut r12
alias initialPressure r11
alias airlookState r10 # 1 - internal state, 0 - external state
alias rFrontAirHashName r9
alias rBackAirHashName r8

Init:
l airlookState DoorInt Open
select r0 airlookState 0 1
sb kPrefHashHangarDoor Open r0
s DoorInt Mode 0 # Set logic mode
s DoorInt On airlookState
sb kPrefHashAirVent Lock 1 # Lock air vent
sb kSlider On 0

Main:
yield
s db Setting airlookState
lb r0 kToggleButton Activate Maximum
beqz r0 Main

sb kFlickerLamp On 1
sb kSlider On 1
sb kSpeaker On 1
sb kSpeaker Mode 22
sleep 1
beq airlookState 0 GoToInt

GoToExt:
lb pressInt kGasSensor Pressure Maximum # save internal pressure
move pressOut pressExt
move rFrontAirHashName kHashNameAirVentExt
move rBackAirHashName kHashNameAirVentInt
s DoorInt Open 0
s DoorInt On 0
jal Start
sb kPrefHashHangarDoor On 1
sb kPrefHashHangarDoor Open 1
yield
sb kPrefHashHangarDoor On 0 # High energy consumption
j Main

GoToInt:
#lb pressExt kGasSensor Pressure Maximum
move pressExt 0
move pressOut pressInt
move rFrontAirHashName kHashNameAirVentInt
move rBackAirHashName kHashNameAirVentExt
sb kPrefHashHangarDoor On 1
sb kPrefHashHangarDoor Open 0
sleep 1
sb kPrefHashHangarDoor On 0
jal Start
sb kSpeaker Mode 25
s DoorInt On 1
s DoorInt Open 1
j Main

Start:
yield
lb initialPressure kGasSensor Pressure Maximum
sbn kPrefHashAirVent rBackAirHashName Mode 1 # set mode depressure
sbn kPrefHashAirVent rBackAirHashName On 1 # START DEPRESSURE
sb kSpeaker Mode 20
WaitDepressure:
yield
lb r0 kSlider On Minimum # check skip stage
beqz r0 Pressurization # If out want open-go next
lb curPress kGasSensor Pressure Maximum # check pressure
div r0 curPress initialPressure
sb kSlider Setting r0
bgtz curPress WaitDepressure # if press != 0 return
Pressurization:
yield
sb kSlider On 1
sbn kPrefHashAirVent rBackAirHashName On 0 # STOP DEPRESSURE
sbn kPrefHashAirVent rFrontAirHashName Mode 0 # First!
sbn kPrefHashAirVent rFrontAirHashName On 1
sb kSpeaker Mode 21
WaitPressurization:
yield
add r0 pressOut kAccuracyPressure # Need pressure
s db Setting r0
sbn kPrefHashAirVent rFrontAirHashName PressureExternal r0
lb r0 kSlider On Minimum # check skip stage
beqz r0 StopPressurization # If door out was open - exit
lb curPress kGasSensor Pressure Minimum
div r0 curPress pressOut
sb kSlider Setting r0
blt curPress pressOut WaitPressurization

StopPressurization:
select airlookState airlookState 0 1
sbn kPrefHashAirVent rFrontAirHashName On 0
yield
sb kSlider On 0
sb kFlickerLamp On 0
j ra