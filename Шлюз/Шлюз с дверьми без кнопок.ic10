define kAccuracyPressure 10
# CONSTANTS ALL DEVICES
define kGASSENSOR -1252983604
define kBUTTON 491845673
define kSLIDER 576516101
define kALARMLAMP -1535893860
define kSENSOR -1713470563
# DEVICES
alias DoorInt d0
alias VentInt d1
alias MainkButton d2
alias DoorExt d3
alias VentExt d4
alias Lamp d5
# REGISTERS
alias pressInt r15
alias pressExt r14
alias curPress r13
alias pressOut r12
alias initialPressure r11
alias airlookState r10
alias sensorWorked r9

Init: # ONCE
l r0 DoorInt Open
select airlookState r0 1 0
select r1 r0 0 1
s DoorExt Open r1
move sensorWorked 1

Main: # CYRCLE
yield
# Update sensors
lb r1 kSENSOR Activate Maximum
select r0 sensorWorked 0 r1
move sensorWorked r1
bgtz r0 Start

#Update buttons
lb r0 kBUTTON Activate Maximum
beqz r0 Main

Start:
select airlookState airlookState 0 1
s db Setting airlookState
sb kALARMLAMP On 1
brdns Lamp 2 # enable lamp if connected
s Lamp On 1

beq airlookState 0 GoExt

GoInt:
lb pressInt kGASSENSOR Pressure Maximum # Save
move pressOut pressExt
alias DoorBack d3
alias DoorFront d0
alias VentBack d4
alias VentFront d1
j CloseDoor

GoExt:
lb pressExt kGASSENSOR Pressure Maximum # Save
move pressOut pressInt
alias DoorBack d0
alias DoorFront d3
alias VentBack d1
alias VentFront d4

CloseDoor:
yield
sb kSLIDER On 1
sb kSLIDER Lock 1
s DoorBack On 1 # enable door in
s DoorBack Open 0 # closed door in
l r0 DoorBack Open # check status door in
bnez r0 CloseDoor # if != closed retern
s DoorBack On 0 # disable door in

StartDepressure:
lb initialPressure kGASSENSOR Pressure Maximum
s VentBack Lock 1
s VentBack Mode 1 # set mode depressure
s VentBack On 1 # START DEPRESSURE
s VentFront Lock 1
s VentFront On 0

WaitDepressure:
yield
l r0 MainkButton Activate # check skip stage
bgtz r0 Pressurization # If out want open-go next
lb curPress kGASSENSOR Pressure Maximum # check pressure
div r0 curPress initialPressure
sb kSLIDER Setting r0
bgtz curPress WaitDepressure # if press != 0 retern

Pressurization:
yield
s VentBack On 0 # STOP DEPRESSURE
s VentFront Mode 0 # First!
s VentFront PressureExternal pressOut
s VentFront On 1
s DoorFront On 1

WaitPressurization:
yield
l r0 MainkButton Activate # check skip stage
bgtz r0 OpenDoor # If door out was open - exit
lb curPress kGASSENSOR Pressure Maximum
sub r1 pressOut kAccuracyPressure # Need pressure
div r0 curPress r1
sb kSLIDER Setting r0
blt curPress r1 WaitPressurization

OpenDoor:
s VentFront On 0
s VentFront Lock 0
s VentBack Lock 0
s DoorFront Open 1
yield
sb kSLIDER On 0
sb kSLIDER Lock 0
sb kALARMLAMP On 0
bdns Lamp Main # disable lamp if connected
s Lamp On 0
j Main