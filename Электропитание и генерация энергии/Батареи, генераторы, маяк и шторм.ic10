define kGeneratorsControl 1
define kEnableCoalGen 0.15 # %
define kDisableCoalGen 0.3 # %
define kEnableGasGen 0.05 # %
define kDisableGasGen 0.5 # %
# WARNING
define kIsLowCharge 0.1 # 10%
define lightStormWarning 60 # Enable flikers
define timeUpdateDisplay 5 # Seconds switching
# BECON
define beaconPowerOn 2 # second powered
define beaconPowerOff 10 # second disabled
# DEVICES
alias Beacon d0
alias Weather d1
alias LevelFire d2
define kLCDPOWER HASH("LCD POWER")
# REGISTERS
alias counterBeacon r15
alias oldCharge r14
alias diffCharge r13
alias powerPercent r12
alias isLowCharge r11
alias energyDecreases r10
alias counterDisplay r9
alias displaySwitch r8
alias counterVoice r7
alias stormIncoming r6
alias combustion r5
alias stormTime r4
# CONSTATNS
define BATTERYLARGE -1388288459
define BATTERYSMALL -400115994
define GENERATORCOAL 813146305
define GENERATORGAS 1165997963
define LCDPOWER -53151617
Main:
yield
lb r0 BATTERYSMALL Charge Sum
lb r1 BATTERYLARGE Charge Sum
add r2 r0 r1 # current charge!
lb r0 BATTERYSMALL Maximum Sum
lb r1 BATTERYLARGE Maximum Sum
add r3 r0 r1 # max charge!
div powerPercent r2 r3 # calculate precent
s db Setting powerPercent
slt energyDecreases r2 oldCharge # new charge < old charge
slt isLowCharge powerPercent kIsLowCharge # < 10%
sub diffCharge r2 oldCharge
round diffCharge diffCharge
move oldCharge r2 # for next tick
BeaconPower:
bdns Beacon SkipBeacon # DISABLED
add counterBeacon counterBeacon 0.5
l r0 Beacon On
select r1 r0 beaconPowerOn beaconPowerOff
blt counterBeacon r1 SkipBeacon
 move counterBeacon 0
 select r0 r0 0 1
 s Beacon On r0
SkipBeacon:
beqz kGeneratorsControl SkipGenerators # DISABLED
lb r0 GENERATORCOAL On Maximum
lb r1 GENERATORGAS On Maximum
select r0 r0 kDisableCoalGen kEnableCoalGen
select r1 r1 kDisableGasGen kEnableGasGen
slt r0 powerPercent r0 # x < y
slt r1 powerPercent r1 # x < y
sb GENERATORCOAL On r0
sb GENERATORGAS On r1
SkipGenerators:
select r0 energyDecreases Color.Orange Color.Green
select r0 isLowCharge Color.Red r0
select r0 stormIncoming Color.Pink r0
sbn LCDPOWER kLCDPOWER Color r0
add counterDisplay counterDisplay 0.5
brlt counterDisplay timeUpdateDisplay 3 # jump if <
 move counterDisplay 0
 select displaySwitch displaySwitch 0 1
select r0 displaySwitch 1 2
select r0 stormIncoming 0 r0
sbn LCDPOWER kLCDPOWER Mode r0
select r0 displaySwitch powerPercent diffCharge
select r0 stormIncoming stormTime r0
sbn LCDPOWER kLCDPOWER Setting r0
WeatherLogic:
bdns Weather Voice # DISABLED
l r2 Weather NextWeatherEventTime
sgtz r0 r2 # time > 0
slt r1 r2 lightStormWarning # <
and stormIncoming r0 r1
CombustionLogic:
move r0 0
lb combustion -1252983604 Combustion Maximum
brdns LevelFire 2
l r0 LevelFire Open
max combustion combustion r0
s db Setting combustion
bnan combustion Voice # Exit if NaN
brdns LevelFire 3
breqz combustion 2 # Disabled only manual!
s LevelFire Open combustion
select r0 combustion 0 1
sb 1736080881 Open r0 # Hangar Door
Voice:
or r2 combustion stormIncoming
sb -1535893860 On r2 # LIGHT WARNING
or r2 r2 isLowCharge
beqz r2 VoicePower
 add counterVoice counterVoice 0.5
 sgt r0 counterVoice 10 # max
 select counterVoice r0 0 counterVoice # reset if max
 slt r0 counterVoice 7
 breqz isLowCharge 2
  select r1 r0 11 23
 breqz stormIncoming 2
  select r1 r0 10 18
 breqz combustion 2
  select r1 r0 5 28
sb -828056979 Mode r1 # ALARM
VoicePower:
sb -828056979 On r2
j Main