# SETTINGS
define kIndexFertilizer 13
define kIndexDrop 12
define kWaitSeeds 1 # 1 - wait seeds, 0 - not wait seeds
# DEVICES
alias dLarry d0
alias dDrop d1
alias dFertilizer d2
define kTrayHash -1841632400
# REGISTERS
alias rNameHash r14
alias rTargetIndex r13
Init:
jal Setup
move r15 sp
sbn -815193061 HASH("LED Display (MaxIndex)") Setting r15
reset:
move sp r15
loop:
sbn -815193061 HASH("LED Display (SettingIndex)") On 0 # DEBUG
beqz sp reset
yield
pop rNameHash
sub rTargetIndex r15 sp # set rTargetIndex
add rTargetIndex rTargetIndex 2 # correct count chutes
brgt rTargetIndex kIndexFertilizer 2
sub rTargetIndex rTargetIndex 1 # corecting
brgt rTargetIndex kIndexDrop 2
sub rTargetIndex rTargetIndex 1 # corecting
sbn -815193061 HASH("LED Display (TargetIndex)") Setting rTargetIndex # DEBUG
s db Setting rTargetIndex
checking:
lbn r0 kTrayHash rNameHash PrefabHash 1
seqz r0 r0
select r1 r0 Color.Red Color.Orange
lbns r0 kTrayHash rNameHash 0 Occupied 1
select r1 r0 Color.Green r1
sbn -815193061 HASH("LED Display (TargetIndex)") Color r1
beqz r0 loop
lbns r0 kTrayHash rNameHash 0 Mature 1
lbns r1 kTrayHash rNameHash 0 Seeding 1
select r1 kWaitSeeds r1 0
add r2 r0 r1
bgt r2 0 harvest
j loop
harvest:
sbn -815193061 HASH("LED Display (SettingIndex)") On 1 # DEBUG
s dLarry Setting rTargetIndex
jal Wait
jal ActivateLarry
lbns r0 kTrayHash rNameHash 0 Mature 1
brgtz r0 -2
DropPlant:
s dLarry Setting kIndexDrop
jal Wait
jal ActivateLarry # DROP PLANT
s dDrop Open 0
ls r0 dLarry 0 Occupied # check hands
lbns r1 kTrayHash rNameHash 0 Occupied 1
and r0 r0 r1
beq r0 1 DropPlant # DROP SEED
fertilizer:
lbns r0 kTrayHash rNameHash 1 Occupied 1 # fertilizer in tray
bgtz r0 plant
 ls r0 dFertilizer 0 Occupied # check fertilizers
 beqz r0 plant # no fertilizers
  s dLarry Setting kIndexFertilizer
  jal Wait
  jal ActivateLarry # take fertilizer
plant:
ls r0 dLarry 0 Occupied # check hands
beqz r0 reset # exit if hands empty
s dLarry Setting rTargetIndex # move to tray
jal Wait
jal ActivateLarry # planting seed or fertilizer
ls r0 dLarry 0 Occupied
brnez r0 -2 # repeat planting seed or fertilizer
j fertilizer
ActivateLarry:
s dLarry Activate 1
Wait:
yield
l r0 dLarry Setting
sbn -815193061 HASH("LED Display (SettingIndex)") Setting r0 # DEBUG
l r0 dLarry Idle
beqz r0 Wait # return if not idle
j ra

Setup:
push HASH("T18")
push HASH("T17")
push HASH("T16")
push HASH("T15")
push HASH("T14")
push HASH("T11")
push HASH("T10")
push HASH("T9")
push HASH("T8")
push HASH("T7")
push HASH("T6")
push HASH("T5")
push HASH("T4")
push HASH("T3")
push HASH("T2")
push HASH("T1")
j ra