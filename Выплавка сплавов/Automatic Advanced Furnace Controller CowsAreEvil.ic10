#Automatic Advanced Furnace Controller CowsAreEvil
alias lever d0
alias button d1
alias hotpump d2
alias coldpump d3
alias hotsensor d4
alias coldsensor d5
define DIAL 554524804
define VEND -443130773
define LED 1944485013
define ADVFURNACE 545937711
alias hotsetting r5
alias coldsetting r6
alias inputspeed r7
alias inputtemp r8
alias inputmoles r9
alias ingothash r10
alias mintemp r11
alias maxtemp r12
alias minpress r13
alias maxpress r14
alias light r15
start:
yield #Start initialise
sb ADVFURNACE SettingOutput 0
s hotpump Setting 0
s coldpump Setting 0
sb LED Color light
move light 2  #Set to green
lb sp DIAL Setting Average #load date
mul sp sp 9
add sp sp 5
pop maxpress
pop minpress
pop maxtemp
pop mintemp
pop ingothash
s db Setting ingothash #End initialise
l r0 button Setting #Start send ingredients
breqz r0 15
lb sp DIAL Setting Average
add sp sp 1
mul sp sp 9
pop r0
sb VEND RequestHash r0
sleep 1
pop r0
sb VEND RequestHash r0
sleep 1
pop r0
sb VEND RequestHash r0
sleep 1
pop r0
sb VEND RequestHash r0 #End send ingredients
lb r0 ADVFURNACE RecipeHash Minimum #Start eject ingots
seq r1 r0 ingothash
seq ingothash ingothash 1
and ingothash ingothash r0
or r1 r1 ingothash
sb ADVFURNACE Open r1 #End eject ingots
l r0 lever Open
beqz r0 start
# Check pressure and temp in alloy zone
lb r0 ADVFURNACE Pressure Maximum #Start close enough
bgt r0 maxpress makeadjustment
blt r0 minpress makeadjustment
lb r0 ADVFURNACE Temperature Maximum
bgt r0 maxtemp makeadjustment
blt r0 mintemp makeadjustment
j start #End close enough
makeadjustment:
#find input gas temp
lb r0 ADVFURNACE TotalMoles Maximum
max r0 r0 0.1
lb r1 ADVFURNACE Pressure Maximum
max r1 r1 0.1
lb r2 ADVFURNACE Temperature Maximum
max r2 r2 0.1
div inputtemp r0 r1
mul inputtemp inputtemp r2
mul inputtemp inputtemp minpress #target energy
div inputmoles inputtemp mintemp #target moles
sub inputmoles inputmoles r0 #moles needed
mul r0 r0 r2 #current energy
sub inputtemp inputtemp r0 #energy needed
div inputtemp inputtemp inputmoles #input temperatur
slez r0 inputmoles
select light r0 6 light
blez inputmoles reducepressure
add inputtemp inputtemp 5
#Calculate cold pump settings
l r0 hotsensor Temperature
sgt r1 inputtemp r0
select light r1 10 light  #Set to pink
sub coldsetting r0 inputtemp
div coldsetting coldsetting r0
l r0 coldsensor Pressure
div coldsetting coldsetting r0
#Calculate hot pump setting
l r0 coldsensor Temperature
slt r1 inputtemp r0
select light r1 11 light  #Set to purple
sub hotsetting inputtemp r0
div hotsetting hotsetting r0
l r0 hotsensor Pressure
div hotsetting hotsetting r0
#Throttle down for target pressure
lb r0 ADVFURNACE Pressure Maximum
sub r0 minpress r0
div r0 r0 70  #input gain
max r0 r0 0
min inputspeed r0 50
add r0 hotsetting coldsetting
div r0 inputspeed r0
mul hotsetting hotsetting r0
mul coldsetting coldsetting r0
#Dump pressure if required
blez coldsetting reducepressure
blez hotsetting reducepressure
#Activate pumps
s coldpump Setting coldsetting
s hotpump Setting hotsetting
add r0 coldsetting hotsetting
sb ADVFURNACE SettingInput r0
j start
reducepressure:
sb ADVFURNACE SettingOutput 50
j start