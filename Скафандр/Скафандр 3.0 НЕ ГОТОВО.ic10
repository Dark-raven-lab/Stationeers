define kVisorAutoOpen 0 # 1=enable, 0=disable
define kTimeoutClosedAfterOpen 5 # seconds
# SUIT ATMO SETTINGS
define kSuitPressureMax 205
define kSuitPressureMin 70
define kSuitPropPressO2 40 # not below min pressure
# EXTERIOR ATMO SETTINGS
define kPressureMax 205
define kPressureMin 30
define kTemperatureMax 50
define kTemperatureMin -15
# DEVICES
alias Suit db
alias Visor d0
# REGISTERS
alias posX r15
alias posZ r14
alias pressure r13
alias ratioO2 r12
alias airEnable r11
alias filterEnable r10
alias visorOpen r9
alias counter r8
Start:
move counter 0
l posX Suit PositionX # save position
l posZ Suit PositionZ # save position
Loop:
yield
l visorOpen Visor Open
l pressure Suit Pressure
l ratioO2 Suit RatioOxygen
#---air & filter control section
mul ratioO2 ratioO2 pressure
sle filterEnable ratioO2 kSuitPropPressO2
select r0 filterEnable 0 1
s Suit On r0 # temperature
l r0 Suit AirRelease
select r0 r0 kSuitPressureMax kSuitPressureMin
sle airEnable pressure r0
#---NaviSy section
l r1 Suit PositionX
l r0 Suit PositionZ
sub r1 r1 posX
mul r1 r1 r1
sub r0 r0 posZ
mul r0 r0 r0
add r0 r0 r1
sqrt r0 r0
add r0 r0 1 # distance
select airEnable visorOpen 0 airEnable
select filterEnable visorOpen 0 filterEnable
select r0 airEnable 999 r0
s Suit PressureSetting r0
s Suit AirRelease airEnable
s Suit Filtration filterEnable
# --- VISOR
l r0 Visor Lock
bgtz r0 Loop # visor locked
l pressure Suit PressureExternal
beqz visorOpen VisorClosed

VisorOpen:
# wait after open
brge counter kTimeoutClosedAfterOpen 3
add counter counter 0.5
j Loop # wait
# EXT PRESSURE
jal CkeckExternalAmto
bgtz r0 Loop # exit if stable
# WARNING
s Visor Open 0
move counter 0
j Loop

VisorClosed:
move counter 0
beqz kVisorAutoOpen Loop # disabled
# EXT PRESSURE
jal CkeckExternalAmto
beqz r0 Loop # exit if fail
s Visor Open 1
j Loop

CkeckExternalAmto:
# Pressure
slt r1 pressure kPressureMax
sgt r2 pressure kPressureMin
and r0 r1 r2 # 1 if good pressure
# Temperature
l r1 Suit TemperatureExternal
sub r1 r1 273.15
slt r2 r1 kTemperatureMax
sgt r1 r1 kTemperatureMin
and r1 r2 r1 # 1 if good temperature
# Logic
and r0 r0 r1 # 1 atmo stable
j ra