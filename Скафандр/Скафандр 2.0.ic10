define kPressIntNorm 101
define TempNorm 22 # Internal temp
# EXTERNAL
define kPressExtMax 150 # max pressure
define kPressExtMin 40 # min pressure
define kTempExtMax 50 # max temp
define kTempExtMin -10 # min temp
define kRationExtOxygen 0.25 # 25%
# Settings for open/close visor
define kAutoVisor 1     # 1=true, 0=false

define MaxPollutantExt 0.05
define MaxVolatilesExt 0.05

# Don't touch
alias Suit db
alias Visor d0
# REGISTERS
alias visorIsOpen r15
alias visorWasOpen r14
alias PressInt r13
alias counter r12
alias needLock r11
alias filtrationOn r10

s Suit PressureSetting kPressIntNorm
add r0 TempNorm 273.15
s Suit TemperatureSetting r0

Main:
yield
l visorIsOpen Visor Open

AutoVisor:
beqz kAutoVisor AutoVisorExit # exit if disable logic
l r0 Visor Lock # read lock
bgtz r0 AutoVisorExit # exit if locked
l r0 Suit PressureExternal
slt r1 r0 kPressExtMax
sgt r2 r0 kPressExtMin
and r0 r1 r2 # 1 if good pressure

l r1 Suit TemperatureExternal
sub r1 r1 273.15
slt r2 r1 kTempExtMax
sgt r1 r1 kTempExtMin
and r1 r2 r1 # 1 if good temperature

and r0 r0 r1 # 1 if (Press & Temp)

l r2 Visor Open
beq r0 r2 AutoVisorExit # exit if this state
s Visor Open r0
j Main
AutoVisorExit:

TemperatureControl:
seqz r0 visorIsOpen # 1 if close
beqz r0 SetTemperature # if == 0
l r0 Suit TemperatureExternal
sub r0 r0 273.15
slt r1 r0 kTempExtMin
sgt r2 r0 kTempExtMax
or r0 r1 r2
SetTemperature:
s Suit On r0

FiltrationControl:
l r1 Suit RatioOxygen
l r0 Suit Filtration
select r2 r0 0.9 0.45
slt r0 r1 r2 # <
select r0 visorIsOpen 0 r0
s Suit Filtration r0
beqz visorIsOpen PressureControl

sgt r1 r1 kRationExtOxygen
l r2 Suit RatioPollutant
slt r2 r2 MaxPollutantExt
l r3 Suit RatioVolatiles
slt r3 r3 MaxVolatilesExt
and r2 r2 r3
and r0 r2 r1
bgtz r0 PressureControl # is good
# not good
s Visor Open 0
sleep 0.5
s Visor Lock 1

PressureControl:
#nor r0 r0 visorIsOpen
select r0 visorIsOpen 0 1
s Suit AirRelease r0

j Main