# Settings pressure control
define kPressMax 130
define kPressNorm 100
define kPressMin 80
# Settings temperature
define TempNorm 22 # const
# Settings filters on/off
define MinRatioIntOxygen 0.45 # 45%
# Settings for open/close visor
define WantOpeningVisor 1 # 1=true, 0=false
define kMinExternalTemp 288 # 18
define kMaxExternalTemp 313 # 40
define MinRationExtOxygen 0.25 # 25%
define MaxPollutantExt 0.005
define MaxVolatilesExt 0.005

# Don't touch
alias Skaf db
alias Visor d0
# REGISTERS
alias ROxygen r15
alias RPollutant r14
alias RVolatiles r13
alias PressExt r12
alias PressInt r11
alias VisorIsOpen r10
alias TempExt r9
alias TempInt r8

s Skaf PressureSetting kPressNorm
add r0 TempNorm 273
s Skaf TemperatureSetting r0

Main:
yield
jal ReadSystem
bgtz VisorIsOpen IsOpen # if Visor Open
# else Visor Closed
beqal WantOpeningVisor 1 AbilityOpenVisor
jal PressureControl
jal OxygenControl
j Main

ReadSystem:
l VisorIsOpen Visor Open
l TempExt Skaf TemperatureExternal
l TempInt Skaf Temperature
l PressInt Skaf Pressure
l PressExt Skaf PressureExternal
l ROxygen Skaf RatioOxygen
l RPollutant Skaf RatioPollutant
l RVolatiles Skaf RatioVolatiles
j ra

IsOpen:
blt PressExt kPressMin VisorClose # if Low
bgt PressExt kPressMax VisorClose # if High
blt ROxygen MinRationExtOxygen VisorClose # if Low
bge RPollutant MaxPollutantExt VisorClose # if High
bge RVolatiles MaxVolatilesExt VisorClose # if High
l r0 Visor Lock
beq r0 1 VisorClose
s Skaf Filtration 0
s Skaf AirRelease 0
s Skaf On 0
j Main
VisorClose:
s Skaf On 1
s Visor Open 0
j Main

AbilityOpenVisor:
l r0 Visor Lock # read Lock
beq r0 1 ra # if lock=true then exit
sgt r0 TempExt kMinExternalTemp # r0 = TempExt>min
slt r1 TempExt kMaxExternalTemp # TempExt = TempExt<max
and r0 r1 r0 # r0 = 1 if (r=3 & r0=1)
sgt PressExt PressExt kPressMin#r0=PressExt.High
and r0 PressExt r0 # r0=1 if (PressExt=1&TempExt=1)
s Visor Open r0
j ra

PressureControl:
l r0 Skaf AirRelease # Get status Air
bgtz r0 AirEnabled # if Air ON
ble PressInt kPressMin AirOn # if <= min
bge PressInt kPressMax AirOn # if >= max
j ra
AirOn:
s Skaf AirRelease 1
j ra
AirEnabled:
sub PressExt kPressNorm PressInt
abs PressExt PressExt # PressExt = |PressExt|
bgt PressExt 5 ra # if op1 > op2
s Skaf AirRelease 0
j ra

OxygenControl:
slt r0 ROxygen MinRatioIntOxygen# if Oxygen.Low
s Skaf Filtration r0
j ra