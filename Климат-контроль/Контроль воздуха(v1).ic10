define TempHysteresis 2
define MinCO2Ratio 0.05
define SetPressure 95
define PressHysteresis 15

alias GasSensor d0
alias ActVent d1
alias RegTemp d2
alias CoolVent d3
alias CoolSens d4
alias Filter d5
alias CoolerEnable r15
alias HeaterEnable r14
alias PressCur r13
alias TempCur r12
alias Min r11
alias Max r10
alias Filling r9
alias Decompression r8
alias Warning r7
move CoolerEnable 0
move HeaterEnable 0
s RegTemp Mode 50
s RegTemp Setting 25
Main:
yield
l PressCur GasSensor Pressure
# Check switch decompression
jal EmergencyRelease
# Temperature Control
jal TempControl
# AirToxin
bdseal Filter AirToxin
# Control Pressure
bdns ActVent Main
jal PressControl
# CO2Control
jal CO2Control
j Main
EmergencyRelease: # Alarm Depressure
l Warning db Setting
sb -1535893860 On Warning # Flashing light
beqz Warning ra # If 0 - exit
sgtz r0 PressCur
s ActVent On r0
s ActVent Mode 1
sb 24258244 On 0 # All Heaters
s Filter On 0
j Main
PressControl:
sub Min SetPressure PressHysteresis
add Max SetPressure PressHysteresis
select Min Filling SetPressure Min
select Max Decompression SetPressure Max
slt Filling PressCur Min # <
sgt Decompression PressCur Max # >
# opt1 = 0 if opt2 == 0 & opt3 == 0
# else 1
or r1 Filling Decompression
beqz r1 ra # if pressure Normal-exit
# else - enable AirVent and return Main
s ActVent On 1
s ActVent Mode Decompression
j Main
CO2Control:
l r0 GasSensor RatioCarbonDioxide # Low CO2
slt r0 r0 MinCO2Ratio # 1 if < norm
s ActVent On r0
s ActVent Mode r0
j ra
AirToxin:
ls r0 Filter 0 Occupied
ls r1 Filter 1 Occupied
add r0 r0 r1
breqz r0 5 # if 0 to Exit
l r0 GasSensor RatioPollutant # High X
l r1 GasSensor RatioVolatiles # Hign H2
add r0 r0 r1
sgtz r0 r0 # if > 0
s Filter On r0
j ra
TempControl:
l TempCur GasSensor Temperature
l r0 RegTemp Setting # TempSet
sub TempCur TempCur 273.15
sub Min r0 TempHysteresis
add Max r0 TempHysteresis
select Min HeaterEnable r0 Min
select Max CoolerEnable r0 Max
slt HeaterEnable TempCur Min # op2 < op3
sgt CoolerEnable TempCur Max # op2 > op3
sb 24258244 On HeaterEnable
s CoolVent Mode CoolerEnable
select r0 CoolerEnable 0 10
s CoolVent PressureExternal r0
l r1 CoolSens Pressure
breqz CoolerEnable 3 # if 0 go
sne r2 r1 r0 # 1 if curPres != setPres
jr 2
slt r2 r1 r0
s CoolVent On r2
j ra