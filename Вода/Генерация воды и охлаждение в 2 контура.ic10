# SETTINGS FOR START COMBUSTER
define kWaterMaxLiters 1000     # Maximum volume of clean water
# SETTINGS COOLING WATER
define kMinPumpL 0.1            # Minimum volume in the cooler
define kWaterMaxTemp 35         # C
define kWaterMinTemp 0          # C
# SETTINGS WASTE GAS
define kGasMinPressure 100      # kPa
define kGasMaxPressure 7000    # kPa
define kGasMaxTemperature 100   # C
# DEVICE LOCK SETTINGS
define kLockDevices 0           # 1 - LOCK, 0 - UNLOCK

# MAIN DEVICES
define kHashCombusterH2 1840108251
alias WaterTankClean d0         # OPTIONAL! Clean water tank
# GAS DEVICES
alias GasPumpWaste d1           # Drop cooler gas

# WATER DEVICES
alias WPumpInCooler d2          # Pumping in cooler
alias WCoolerAnalyzer d3        # Pipe analyzer
alias WPumpOutCooler d4         # Pumping water from tne cooler
alias WPurgeCooler d5           # Purge water from the cooler

# REGISTERS
alias rGasPressure r15

Init:
sb kHashCombusterH2 On 1
sb kHashCombusterH2 Mode 0
s WCoolerAnalyzer Lock kLockDevices
s WCoolerAnalyzer On 1
s WPumpOutCooler Lock kLockDevices
s WPumpOutCooler Setting 10
s GasPumpWaste Lock kLockDevices
s GasPumpWaste Setting 10
s WPumpInCooler Lock kLockDevices
s WPumpInCooler Setting 1
brdns WPurgeCooler 3
s WPurgeCooler Lock kLockDevices
s WPurgeCooler Setting 0

Main:
yield
lb rGasPressure kHashCombusterH2 PressureOutput Maximum

# GAS WASTE PUMPING
sgt r0 rGasPressure 45000                       # Enable drop gas if pressure critical
bgtz r0 SetGasPump
ble rGasPressure kGasMinPressure SetGasPump
lb r1 kHashCombusterH2 TemperatureOutput Maximum
sub r1 r1 273.15
bgt r1 kGasMaxTemperature SetGasPump        # Pump is disabled if temperature high
lb r1 kHashCombusterH2 RatioSteamOutput Maximum
bgtz r1 SetGasPump                          # Pump is disabled if water steam > 0
lb r1 kHashCombusterH2 RatioWaterOutput Maximum
sle r0 r1 0                                 # Pump is enabled if water = 0
SetGasPump:
s GasPumpWaste On r0
s WPumpInCooler On r0

# COMBUSTER LOGIC
lb r0 kHashCombusterH2 Mode Maximum
# Determine the pressure threshold based on CombusterH2 state
select r1 r0 kGasMaxPressure kGasMinPressure
# Check pressure against the determined threshold
slt r0 rGasPressure r1

#l r1 WaterTankClean VolumeOfLiquid
#slt r1 r1 kWaterMaxLiters

# Set CombusterH2 state based on the pressure check
sb kHashCombusterH2 Mode r0

# COOLER WATER PUMPING
move r0 0
l r1 WCoolerAnalyzer VolumeOfLiquid     # Get volume of water in the cooler
blt r1 kMinPumpL SetWaterPump           # Pump is disabled if water low

l r1 WCoolerAnalyzer Temperature        # Get temperature of water
sub r1 r1 273.15

bgt r1 kWaterMaxTemp SetWaterPump       # Pump is disabled if temperature high
blt r1 kWaterMinTemp SetWaterPump       # Pump is disabled if temperature low

l r1 WCoolerAnalyzer RatioPollutant
l r2 WCoolerAnalyzer RatioLiquidPollutant
max r2 r1 r2
sgtz r2 r2                              # Purge is enabled if water is polluted
seq r0 r2 0                             # Pump is enabled if water is clean
SetWaterPump:
s WPumpOutCooler On r0                  # Pumping water from the cooler
brdns WPurgeCooler 2
s WPurgeCooler On r2                    # Purge water from the cooler

j Main